apply plugin: "java"

sourceCompatibility = javaSourceCompatibility
targetCompatibility = javaTargetCompatibility

repositories {
   mavenCentral()
}

def includeManifest = {
   manifest {
      attributes "Specification-Title": projectName,
                 "Specification-Version": specVersion,
                 "Implementation-Version": releaseVersion,
                 "Built-By": builtByName == null || builtByName.trim().isEmpty() ? System.getProperty( "user.name" ) : builtByName,
                 "Built-Date": new Date(),
                 "Built-JDK": System.getProperty( "java.version" ),
                 "Built-Gradle": gradle.gradleVersion
   }
}

jar {
   dependsOn "test"

   configure includeManifest
   archiveName = jarName + "-" + releaseVersion + ".jar"
}

javadoc {
   options.addBooleanOption( "html5", true )
}

task sourcesJar( type:  Jar ) {
   classifier "sources"
   from sourceSets.main.allSource

   configure includeManifest
   archiveName = jarName + "-sources-" + releaseVersion + ".jar"
}

task javadocJar( type: Jar, dependsOn: javadoc ) {
   classifier = "javadoc"
   from javadoc.destinationDir

   configure includeManifest
   archiveName = jarName + "-javadoc-" + releaseVersion + ".jar"
}

task jars {
   dependsOn "clean"
   dependsOn "jar"
   dependsOn "sourcesJar"
   dependsOn "javadocJar"
}

artifacts {
   archives sourcesJar
   archives javadocJar
}
